apply plugin: 'com.android.application'

Properties prop = new Properties()
prop.load(new FileInputStream(file("signing.properties")))

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId project.GROUP
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_MIN_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
        // 默认是debug的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "DEBUG"]
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file(prop['KEY_FILE'])
            storePassword prop['STORE_PASSWORD']
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            //是否输出LOG信息
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //设置是否混淆
            minifyEnabled true
            zipAlignEnabled true
            //移除无用的资源文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为AppPlus_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "AppPlus_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
        debug {
            //是否输出LOG信息
            buildConfigField "boolean", "LOG_DEBUG", "true"
        }
    }

    //多渠道打包
    productFlavors {
        xiaomi {}
        m360 {}
        wandoujia {}
        baidu {}
        m91 {}
        anzhuo {}
        fir {}
        umeng {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.jenzz:materialpreference:1.3'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'com.android.support:gridlayout-v7:22.2.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}
