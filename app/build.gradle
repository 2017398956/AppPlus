apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'

Properties prop = new Properties()
prop.load(new FileInputStream(file("signing.properties")))

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_COMPILE_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId project.GROUP
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_MIN_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
        // 默认是umeng的渠道
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "DEBUG"]
    }

    signingConfigs {
        release {
            storeFile file(prop['KEY_FILE'])
            storePassword prop['STORE_PASSWORD']
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
        }
    }

    buildTypes {
        release {
            //设置是否混淆
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug{
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    //多渠道打包
    productFlavors {
        xiaomi {}
        m360 {}
        wandoujia {}
        baidu {}
        m91 {}
        anzhuo {}
        fir {}
        weibo {}
        umeng {}
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.afollestad:material-dialogs:0.7.6.0'
    compile 'com.jenzz:materialpreference:1.3'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'com.android.support:gridlayout-v7:22.2.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
}
